/*
 * Copyright 2018 NEM
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    }
}

plugins {
    id 'jvm-component'
    id 'java-lang'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'net.saliman.properties' version '1.4.6'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'org.junit.platform.gradle.plugin'


group 'io.nem'
version '0.8.10-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    sharedCompile
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}


def integrationTest = task('integrationTest',
        type: JavaExec,
        group: 'Verification') {
    description = 'Runs integration tests.'
    dependsOn testClasses
    shouldRunAfter test
    classpath = sourceSets.integrationTest.runtimeClasspath

    main = 'org.junit.platform.console.ConsoleLauncher'
    args = ['--scan-class-path',
            sourceSets.integrationTest.output.classesDir.absolutePath,
            '--reports-dir', "${buildDir}/test-results/junit-integrationTest"]
}

check {
    dependsOn integrationTest
}

ext {
    vertxVersion = "3.5.0"
    rxjavaVersion = "2.1.7"
    junitVersion = "5.0.2"
}

dependencies {
    // Reactive
    compile "io.vertx:vertx-core:${vertxVersion}"
    compile "io.vertx:vertx-web-client:${vertxVersion}"
    compile "io.vertx:vertx-rx-java2:${vertxVersion}"
    compile "io.reactivex.rxjava2:rxjava:${rxjavaVersion}"

    // Log
    compile 'org.apache.logging.log4j:log4j-core:2.5'
    compile 'org.apache.logging.log4j:log4j-jul:2.5'

    compile 'org.bouncycastle:bcprov-jdk15on:1.58'
    compile 'commons-codec:commons-codec:1.11'
    compile 'commons-lang:commons-lang:2.6'
    compile 'commons-io:commons-io:2.4'
    compile 'net.minidev:json-smart:1.2'
    compile 'org.apache.commons:commons-math3:3.2'
    compile 'org.apache.httpcomponents:httpasyncclient:4.1.1'
    compile 'com.googlecode.javaewah:JavaEWAH:1.0.0'
    compile "com.google.flatbuffers:flatbuffers-java:1.8.0"
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'io.swagger:swagger-parser:1.0.34'

    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.googlecode.matrix-toolkits-java:mtj:1.0.1'

    // Tests
    testCompile 'org.slf4j:slf4j-simple:1.7.25'
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testCompile "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testCompile 'org.apiguardian:apiguardian-api:1.0.0'
    testCompile 'org.junit.platform:junit-platform-console-standalone:1.1.0-M1'
}

junitPlatform {
    platformVersion '1.0.2' // optional, defaults to plugin version
    logManager 'org.apache.logging.log4j.jul.LogManager'
    reportsDir file('build/test-results/junit-platform') // this is the default
    enableStandardTestTask true
    // selectors (optional)
    // filters (optional)
}

shadowJar {
    appendix = ""
    dependencies {
        exclude(dependency {
            it.moduleGroup != 'io.nem'
        })
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId 'io.nem'
            artifactId 'sdk'
            pom.withXml {
                asNode().with {
                    appendNode('packaging', 'jar')
                    appendNode('name', 'NEM2-SDK Java')
                    appendNode('description', 'nem2 java sdk')
                    appendNode('url', 'https://github.com/nemtech/nem2-sdk-java')
                    appendNode('issueManagement').with {
                        appendNode('url', 'https://github.com/nemtech/nem2-sdk-java/issues')
                        appendNode('system', 'GitHub Issues')
                    }
                    appendNode('licenses').with {
                        appendNode('license').with {
                            appendNode('name', 'APACHE-2.0')
                            appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0')
                        }
                    }
                    appendNode('organization').with {
                        appendNode('name', 'NEM')
                        appendNode('url', 'https://nem.io')
                    }
                    appendNode('developers').with {
                        appendNode('developer').with {
                            appendNode('id', 'aleix_mp')
                            appendNode('name', 'aleix')
                            appendNode('email', 'aleix@nemeurope.eu')
                            appendNode('organization', 'NEM')
                            appendNode('organizationUrl', 'https://nem.io')
                        }
                        appendNode('developer').with {
                            appendNode('id', 'guillemchain')
                            appendNode('name', 'guillem')
                            appendNode('email', 'guillem@nemeurope.eu')
                            appendNode('organization', 'NEM')
                            appendNode('organizationUrl', 'https://nem.io')
                        }
                    }
                }
            }
            tasks.withType(Jar) {
                from(project.projectDir) {
                    include 'LICENSE'
                    into 'META-INF'
                }
            }
            artifact sourceJar
            artifact javadocJar
        }
    }
    repositories {

    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

junitPlatform {
    platformVersion '1.0.2' // optional, defaults to plugin version
    logManager 'org.apache.logging.log4j.jul.LogManager'
    reportsDir file('build/test-results/junit-platform') // this is the default
    // enableStandardTestTask true
    // selectors (optional)
    // filters (optional)
}